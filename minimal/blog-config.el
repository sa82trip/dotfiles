;;; blog-config.el --- Org Mode ë¸”ë¡œê·¸ ìš´ì˜ ì„¤ì • ---

;; ì´ íŒŒì¼ì€ Emacs Org Modeë¥¼ ì‚¬ìš©í•˜ì—¬ ë¸”ë¡œê·¸ë¥¼ ìš´ì˜í•˜ê¸° ìœ„í•œ
;; ëª¨ë“  ê´€ë ¨ ì„¤ì •ì„ í¬í•¨í•©ë‹ˆë‹¤.

(defvar vimacs/blog-org-directory (expand-file-name "~/blog/org/")
  "ë¸”ë¡œê·¸ org íŒŒì¼ë“¤ì´ ì €ì¥ëœ ê¸°ë³¸ ë””ë ‰í† ë¦¬ì…ë‹ˆë‹¤.")

(defvar vimacs/blog-posts-directory (concat vimacs/blog-org-directory "posts/")
  "ë¸”ë¡œê·¸ í¬ìŠ¤íŠ¸ íŒŒì¼ë“¤ì´ ì €ì¥ëœ ë””ë ‰í† ë¦¬ì…ë‹ˆë‹¤.")

(defvar vimacs/blog-template-directory (concat vimacs/blog-org-directory "templates/")
  "ë¸”ë¡œê·¸ í…œí”Œë¦¿ íŒŒì¼ë“¤ì´ ì €ì¥ëœ ë””ë ‰í† ë¦¬ì…ë‹ˆë‹¤.")

;; ë””ë ‰í† ë¦¬ ìƒì„± í™•ì¸
(unless (file-exists-p vimacs/blog-posts-directory)
  (make-directory vimacs/blog-posts-directory t))

(unless (file-exists-p vimacs/blog-template-directory)
  (make-directory vimacs/blog-template-directory t))

;; org íŒŒì¼ë“¤ì„ ì´ìš©í•´ì„œ htmlì„ ë§Œë“¤ê¸° ìœ„í•œ ì„¤ì •ë“¤
(setq org-publish-project-alist
      `(("blog-org"
         :base-directory ,vimacs/blog-org-directory
         :base-extension "org"
         :publishing-directory "~/blog/public/"
         :recursive t
         :exclude "\\.Trash\\|\\.git\\|\\.DS_Store"
         :publishing-function org-html-publish-to-html
         :with-toc t                    ; ëª©ì°¨ ì¶œë ¥
         :section-numbers nil           ; ì„¹ì…˜ ë„˜ë²„ ê°ì¶¤
         :htmlized-source t             ; ì½”ë“œ í•˜ì´ë¼ì´íŠ¸
         :auto-preamble t
         :with-author nil               ; Don't include author name
         :html-head-extra "<header>\12    <nav style=\"text-align:center; margin-bottom:2em;\">\12      <a href=\"/index.html\">ğŸ  í™ˆ</a> |\12      <a href=\"/about.html\">ğŸ‘¤ ì†Œê°œ</a> |\12      <a href=\"/posts/\">ğŸ“š ì „ì²´ê¸€</a>\12    </nav>\12   </header>"
         :with-creator t)   ; Include Emacs and Org versions in footer
        ("blog-static"
         :base-directory ,(concat vimacs/blog-org-directory "static/")
         :base-extension "jpg\\|jpeg\\|gif\\|png\\|svg"
         :publishing-directory "~/blog/public/static/"
         :recursive t
         :publishing-function org-publish-attachment)
        ("blog-assets"
         :base-directory "~/blog/assets/"
         :base-extension "css\\|js"
         :publishing-directory "~/blog/public/assets/"
         :recursive t
         :publishing-function org-publish-attachment)
        ("blog"
         :components ("blog-org" "blog-static" "blog-assets"))))

;; Customize the HTML output
(setq org-html-validation-link nil      ; Don't show validation link
      org-html-head-include-scripts nil ; Use our own scripts
      org-html-head-include-default-style nil ; Use our own styles
      org-html-head "<link rel=\"stylesheet\" href=\"https://gongzhitaao.org/orgcss/org.css\" />
                     <script defer src=\"/assets/search.js\"></script>")

;; simpleí•œ ì„œë²„ë¥¼ ë„ì›Œì„œ ë¸”ë¡œê·¸ í™•ì¸
;; (use-package simple-httpd :ensure t)
(setq httpd-root "~/blog/public")
;; (httpd-start)

;; HTML íŠ¹ìˆ˜ë¬¸ì ì´ìŠ¤ì¼€ì´í”„ í•¨ìˆ˜
(defun vimacs/escape-html (string)
  "HTML íŠ¹ìˆ˜ë¬¸ìë¥¼ ì´ìŠ¤ì¼€ì´í”„í•©ë‹ˆë‹¤."
  (let ((replacements '(("&" . "&amp;")
                       ("<" . "&lt;")
                       (">" . "&gt;")
                       ("\"" . "&quot;")
                       ("'" . "&#39;"))))
    (dolist (pair replacements string)
      (setq string (replace-regexp-in-string (car pair) (cdr pair) string)))))

;; í‚¤ì›Œë“œ ê°’ì„ ê°€ì ¸ì˜¤ëŠ” ë„ìš°ë¯¸ í•¨ìˆ˜
(defun vimacs/org-get-keyword (keyword)
  "í˜„ì¬ ë²„í¼ì—ì„œ Org í‚¤ì›Œë“œ(#+KEYWORD: value)ì˜ ê°’ì„ ë°˜í™˜í•©ë‹ˆë‹¤."
  (let ((case-fold-search t)) ; ëŒ€ì†Œë¬¸ì êµ¬ë¶„ ì—†ì´ ê²€ìƒ‰
    (save-excursion
      (goto-char (point-min))
      (when (re-search-forward (format "^#\\+%s: *\\(.*\\)" keyword) nil t)
        (match-string-no-properties 1)))))

;; íŒŒì¼ ìˆ˜ì • ì‹œê°„ìœ¼ë¡œ ì •ë ¬í•˜ëŠ” í•¨ìˆ˜
(defun vimacs/sort-files-by-mtime (files)
  "íŒŒì¼ ëª©ë¡ì„ ìˆ˜ì • ì‹œê°„ ê¸°ì¤€ìœ¼ë¡œ ì •ë ¬í•©ë‹ˆë‹¤ (ìµœì‹  íŒŒì¼ì´ ë¨¼ì €)."
  (sort files
        (lambda (a b)
          (time-less-p (file-attribute-modification-time (file-attributes b))
                       (file-attribute-modification-time (file-attributes a))))))

(defun vimacs/get-post-data (file)
  "org íŒŒì¼ì—ì„œ í¬ìŠ¤íŠ¸ ë©”íƒ€ë°ì´í„°(ì œëª©, ë‚ ì§œ, ë§í¬ ë“±)ë¥¼ ì¶”ì¶œí•©ë‹ˆë‹¤."
  (with-temp-buffer
    (insert-file-contents file)
    (when (not (equal (vimacs/org-get-keyword "DRAFT") "TRUE"))
      (let* ((title (or (vimacs/org-get-keyword "TITLE") "ì œëª© ì—†ìŒ"))
             (date-raw (or (vimacs/org-get-keyword "DATE") ""))
             (date (replace-regexp-in-string "[<>]" "" date-raw))
             (filename (file-name-nondirectory (file-name-sans-extension file)))
             (link (format "./posts/%s.html" filename)))
        `((title . ,title)
          (date . ,date)
          (link . ,link))))))

(defun vimacs/format-post-html (post-data)
  "í¬ìŠ¤íŠ¸ ë°ì´í„°ë¥¼ HTML ë¦¬ìŠ¤íŠ¸ ì•„ì´í…œ í˜•ì‹ìœ¼ë¡œ ë³€í™˜í•©ë‹ˆë‹¤."
  (let* ((title (cdr (assoc 'title post-data)))
         (title-escaped (vimacs/escape-html title))
         (link (cdr (assoc 'link post-data)))
         (date (cdr (assoc 'date post-data))))
    (format "<li data-title=\"%s\"><a href=\"%s\">%s</a> (%s)</li>\n"
            title-escaped link title-escaped date)))

(defun vimacs/update-index-with-posts ()
  "posts/ ë””ë ‰í† ë¦¬ì˜ org íŒŒì¼ë“¤ì„ ì½ì–´ index.orgì— ìµœì‹  ê¸€ ëª©ë¡ì„ ì—…ë°ì´íŠ¸í•©ë‹ˆë‹¤."
  (interactive)
  (let* ((index-file (concat vimacs/blog-org-directory "index.org"))
         (post-files (vimacs/sort-files-by-mtime
                      (directory-files vimacs/blog-posts-directory t "\.org$")))
         (posts-data (cl-remove-if #'null (mapcar #'vimacs/get-post-data post-files)))
         (post-list-html (s-join "" (mapcar #'vimacs/format-post-html posts-data))))
    (with-temp-buffer
      (insert
       "#+TITLE: Vimacs's ã¾ãªã¶ã“ã¨\n"
       "#+AUTHOR: Vimacs\n\n"
       "#+BEGIN_EXPORT html\n"
       "<input type=\"text\" id=\"search-input\" placeholder=\"Search\">
"
       "#+END_EXPORT\n\n"
       "ìµœì‹  ê¸€ ëª©ë¡:\n\n"
       "#+BEGIN_EXPORT html\n"
       "<ul id=\"post-list\">\n"
       post-list-html
       "</ul>\n"
       "<p id=\"no-results\" style=\"display: none;\">ê²€ìƒ‰ ê²°ê³¼ê°€ ì—†ìŠµë‹ˆë‹¤.</p>\n"
       "#+END_EXPORT\n")
      (write-file index-file))
    (message "Index í˜ì´ì§€ê°€ ì—…ë°ì´íŠ¸ë˜ì—ˆìŠµë‹ˆë‹¤.")))

(defun vimacs/new-blog-post (title)
  "posts í´ë”ì— ìƒˆë¡œìš´ ë¸”ë¡œê·¸ í¬ìŠ¤íŠ¸ë¥¼ í…œí”Œë¦¿ íŒŒì¼ì„ ê¸°ë°˜ìœ¼ë¡œ ë§Œë“¤ì–´ ì—½ë‹ˆë‹¤."
  (interactive "sPost title: ")
  (let* ((slug (downcase title))
         ;; 1. í•œê¸€ ë° í—ˆìš©ëœ ë¬¸ì ì™¸ì˜ ê²ƒë“¤ì„ í•˜ì´í”ˆìœ¼ë¡œ ë³€ê²½
         (slug (replace-regexp-in-string "[^a-z0-9_ê°€-í£-]" "-" slug))
         ;; 2. ì—¬ëŸ¬ ê°œì˜ í•˜ì´í”ˆì„ í•˜ë‚˜ë¡œ ì¶•ì•½
         (slug (replace-regexp-in-string "--+" "-" slug))
         ;; 3. ë§¨ ì•/ë’¤ í•˜ì´í”ˆ ì œê±°
         (slug (replace-regexp-in-string "^-\\|-$" "" slug))
         (date (format-time-string "%Y-%m-%d"))
         (filename (format "%s-%s.org" date slug))
         (full-path (expand-file-name filename vimacs/blog-posts-directory))
         (template-path (expand-file-name "blog-post-template.org" vimacs/blog-template-directory)))
    (if (file-exists-p full-path)
        (message "ì´ë¯¸ ê°™ì€ ì´ë¦„ì˜ í¬ìŠ¤íŠ¸ê°€ ì¡´ì¬í•©ë‹ˆë‹¤: %s" filename)
      (progn
        ;; í…œí”Œë¦¿ íŒŒì¼ì´ ì—†ìœ¼ë©´ ê¸°ë³¸ í…œí”Œë¦¿ ìƒì„±
        (unless (file-exists-p template-path)
          (with-temp-file template-path
            (insert "#+TITLE: {{TITLE}}\n"
                    "#+DATE: <{{DATE}}>\n"
                    "#+AUTHOR: Vimacs\n"
                    "#+DRAFT: TRUE\n\n"
                    "* ì„œë¡ \n\n"
                    "* ë³¸ë¡ \n\n"
                    "* ê²°ë¡ \n")))
        ;; ìƒˆ í¬ìŠ¤íŠ¸ íŒŒì¼ ìƒì„±
        (find-file full-path)
        ;; í…œí”Œë¦¿ íŒŒì¼ ë‚´ìš©ì„ ë²„í¼ì— ì‚½ì…
        (insert-file-contents template-path)
        ;; ë²„í¼ ì „ì²´ë¥¼ ëŒ€ìƒìœ¼ë¡œ í”Œë ˆì´ìŠ¤í™€ë”ë¥¼ ì‹¤ì œ ê°’ìœ¼ë¡œ ì¹˜í™˜
        (goto-char (point-min))
        (while (search-forward "{{TITLE}}" nil t)
          (replace-match title nil t))
        (goto-char (point-min))
        (while (search-forward "{{DATE}}" nil t)
          (replace-match date nil t))
        (save-buffer)
        (message "Created new post: %s" filename)))))

;; ë¸”ë¡œê·¸ ë°œí–‰ í•¨ìˆ˜
(defun vimacs/publish-blog ()
  "ë¸”ë¡œê·¸ í¬ìŠ¤íŠ¸ë“¤ì„ HTMLë¡œ ë°œí–‰í•˜ê³  ì¸ë±ìŠ¤ë¥¼ ì—…ë°ì´íŠ¸í•©ë‹ˆë‹¤."
  (interactive)
  (vimacs/update-index-with-posts)
  (org-publish "blog" t)
  (message "ë¸”ë¡œê·¸ê°€ ë°œí–‰ë˜ì—ˆìŠµë‹ˆë‹¤."))

;; í¸ì˜ í•¨ìˆ˜ë“¤
(defun vimacs/blog-open-index ()
  "ë¸”ë¡œê·¸ ì¸ë±ìŠ¤ íŒŒì¼ì„ ì—½ë‹ˆë‹¤."
  (interactive)
  (find-file (concat vimacs/blog-org-directory "index.org")))

(defun vimacs/blog-dired-posts ()
  "posts ë””ë ‰í† ë¦¬ë¥¼ diredë¡œ ì—½ë‹ˆë‹¤."
  (interactive)
  (dired vimacs/blog-posts-directory))

(provide 'blog-config)
;;; blog-config.el ends here



